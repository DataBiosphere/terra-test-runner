plugins {
    id 'java'
    id 'idea'
    id 'java-gradle-plugin'
    id 'java-library'
    id 'maven-publish'

    id 'com.jfrog.artifactory' version '4.28.2'
    id 'com.github.spotbugs' version '5.0.12'
    id 'com.diffplug.spotless' version '6.10.0'
}

// TODO(PF-1981) - Move version to settings.gradle
group 'bio.terra'
version '0.1.8-SNAPSHOT'

gradlePlugin {
    plugins {
        testRunnerPlugin {
            id = 'bio.terra.test-runner-plugin'
            implementationClass = 'bio.terra.testrunner.plugin.TestRunnerPlugin'
        }
    }
}

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false
repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    google()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    // Common utils
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

    // JSON processing
    ext {
        jackson = '2.13.4'
    }
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: "${jackson}"
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: "${jackson}"

    // Misc. Services
    implementation group: 'io.kubernetes', name: 'client-java', version: '18.0.0'
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '6.3.0.202209071007-r'
    implementation group: 'org.graalvm.compiler', name: 'compiler', version: '22.2.0'

    // Google dependencies
    implementation platform('com.google.cloud:libraries-bom:26.0.0') // use common bom
    implementation group: 'com.google.cloud', name: 'google-cloud-bigquery'
    implementation group: 'com.google.cloud', name: 'google-cloud-logging'
    implementation group: 'com.google.cloud', name: 'google-cloud-monitoring'
    implementation group: 'com.google.auth', name: 'google-auth-library-oauth2-http'
    implementation group: 'com.google.cloud', name: 'google-cloud-storage'
    implementation group: 'com.google.apis', name: 'google-api-services-container', version: 'v1-rev93-1.25.0'

    // Logging
    ext {
        apacheLog = '2.18.0'
    }
    // Force transitive log4j version to at least 2.17.0 for security concerns
    implementation('org.apache.logging.log4j:log4j-api') {
        version {
            require "${apacheLog}"
        }
    }
    implementation('org.apache.logging.log4j:log4j-to-slf4j') {
        version {
            require "${apacheLog}"
        }
    }
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.1'

    // Spotbugs
    implementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.7.2'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

// for scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

apply from: "$rootDir/gradle/dependency-locking.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/spotbugs.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/test.gradle"
