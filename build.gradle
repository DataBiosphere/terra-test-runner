plugins {
    id 'idea'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.17.2'
    id 'com.diffplug.spotless' version '5.5.1'
    //id 'org.springframework.boot' version '2.3.4.RELEASE'
}

sourceCompatibility = JavaVersion.VERSION_11

group 'bio.terra'
version '0.0.1-SNAPSHOT'

def artifactory_username = System.getenv('ARTIFACTORY_USERNAME')
def artifactory_password = System.getenv('ARTIFACTORY_PASSWORD')

gradlePlugin {
    plugins {
        testRunnerPlugins {
            id = 'bio.terra.test-runner-plugin'
            implementationClass = 'bio.terra.testrunner.plugin.EchoPlugin'
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    google()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
        credentials {
            username = "${artifactory_username}"
            password = "${artifactory_password}"
        }
    }
}

dependencies {
    implementation 'com.google.guava:guava:29.0-jre'
    implementation group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.11.2"
    implementation group: "javax.annotation", name: "javax.annotation-api", version: "1.3.2"

    implementation group: 'org.springframework.boot', name: 'spring-boot', version: '2.3.4.RELEASE'
    implementation group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: '2.3.4.RELEASE'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.3.4.RELEASE'
    implementation group: "org.springframework", name: "spring-context", version: "5.2.9.RELEASE"

    testCompile group: 'junit', name: 'junit', version: '4.12'

    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-engine", version: "5.6.2"
    testImplementation group: "org.springframework", name: "spring-test", version: "5.2.9.RELEASE"

    annotationProcessor group: "org.springframework.boot", name: "spring-boot-configuration-processor", version: "2.3.4.RELEASE"
}

// Exclude the Spring logger, so everything will use SLF4J
configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped"]
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}

publishing {
    publications {
        testRunnerLibrary(MavenPublication) {
            from components.java
            versionMapping {
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
        }
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(artifactoryPublish) &&
            (artifactory_username == null || artifactory_password == null)) {
        throw new GradleException("Set env vars ARTIFACTORY_USERNAME and ARTIFACTORY_PASSWORD to publish")
    }
}

artifactory {
    contextUrl = 'https://broadinstitute.jfrog.io/broadinstitute'
    publish {
        contextUrl = 'https://broadinstitute.jfrog.io/broadinstitute'
        repository {
            repoKey = 'libs-snapshot-local'
            username = "${artifactory_username}"
            password = "${artifactory_password}"
        }
        defaults {
            publications('testRunnerLibrary')
            publishArtifacts = true
            publishPom = true
        }
    }
}

compileJava.dependsOn spotlessApply